#!/usr/bin/env fish

# https://wiki.debian.org/SecureBoot/VirtualMachine
# https://macroform-node.medium.com/building-a-windows-11-vm-with-qemu-using-tpm-emulation-for-research-malware-analysis-part-1-8846378b9582
# https://ubuntu.com/server/docs/gpu-virtualization-with-qemu-kvm - Suggestion for -device for GPU passthrough settings
# Turn off Windows Fast Start: HKEY_LOCAL_MACHINE\SYSTEM\CurrentControlSet\Control\Session Manager\Power -> HiberbootEnabled -> 0
# lsblk -o NAME,LABEL,MODEL,SERIAL
# lshw -short # Nice list of all devices and peripherals
# sudo lshw -businfo # Devices and connected bus (e.g. 0000:04:00.0)

set MACHINE $argv[1]
set install $argv[2]

if test "$MACHINE" = "seedling"
  set enable_vfio true
  set enable_ovmf true
  set enable_tpm false
  set os "nixos"

  set SERIAL "deadbeee"
  set MAC "84:1b:76:c9:01:a6"
  set OVMF_DIR "$OVMF_NIX_DIR"
else if test "$MACHINE" = "sapling"
  set enable_vfio true
  set enable_ovmf true
  set enable_tpm true
  set os "windows"

  set SERIAL "deadbeef"
  set MAC "84:1b:76:c9:01:a5"
  set OVMF_DIR "$OVMF_MS_DIR"
else
  echo "Usage: $argv[0] <machine> [install]"
  echo " machine: sapling or seedling"
  echo " install: Adding `install` will include the required installation ISOs"
  exit 1
end

if test "$install" = "install"
  set cdrom "$dir/cdrom-$os.qemu"
else
  set cdrom ""
end


# Provides UEFI support for VMs
if $enable_ovmf
  if test -z "$OVMF_DIR" || test -z "$OVMF_MS_DIR"
    echo "Expected \$OVMF_DIR and \$OVMF_MS_DIR to be defined in qemu.nix"
    exit 1
  end
end

set dir (status dirname)
set QEMU qemu-system-x86_64
set tpm_dir /data/code/nixfiles/tmp/emulated_tpm
set TPM_SOCK $tpm_dir/swtpm-sock
set OVMF_CODE $OVMF_DIR/OVMF_CODE.fd
set OVMF_VARS OVMF_VARS.ms.fd

set GPU "0000:01:00.0"
set GPU_AUDIO "0000:01:00.1"
set USB_CONTROLLER "0000:07:00.0"
set GAMES_SSD "0000:08:00.0"

# CPU Pinning - currently off (taskset commented out) while testing with all cores
set CPUS 32
set CORES (math $CPUS / 2)
#set HOST_CORES "0,16,17"
set VM_CORES "" #"taskset -c 1-15,18-31 " # lscpu -e
set RAM 24

# https://www.qemu.org/docs/master/system/i386/hyperv.html
set CPU_PARAMS hv_relaxed,hv_vapic,hv-vpindex,hv-runtime,hv-time,hv-synic,\
hv-stimer,hv-tlbflush,hv-frequencies,hv-apicv,hv_spinlocks=0x1fff,\
hv_vendor_id=0xDEADBEEFFF

# Variables that will be substituted in vm.qemu
set qemu_vars QEMU VM_CORES CPUS CORES CPU_PARAMS RAM TPM_SOCK GPU GPU_AUDIO \
  USB_CONTROLLER GAMES_SSD OVMF_CODE OVMF_VARS HUGEPAGES_PARAMS MACHINE SERIAL \
  MAC

# Bind PCI devices
if $enable_vfio
  echo "Binding devices to VFIO"
  sudo $dir/vfio-bind $USB_CONTROLLER vfio-pci 22

  if test "$os" = "windows"
    sudo $dir/vfio-bind $GAMES_SSD vfio-pci 23
  end
end

# OVMF vars need to be writable but Nix store is readonly
if $enable_ovmf && not test -f tmp/$OVMF_VARS
  cp $OVMF_MS_DIR/$OVMF_VARS tmp/$OVMF_VARS
  chmod +w tmp/$OVMF_VARS
end

# Create a virtual drive for the machine if one doesn't exist
if not test -f /data/vdisks/$MACHINE.qcow2
  qemu-img create -f qcow2 /data/vdisks/$MACHINE.qcow2 300G
end

# Setup TPM emulation
if $enable_tpm
  rm -rf $tpm_dir
  test -d $tpm_dir || mkdir -p $tpm_dir
  swtpm socket --tpmstate dir=$tpm_dir --ctrl type=unixio,path=$TPM_SOCK \
    --log level=20 --tpm2 -d
end

# Remove comments and empty lines
set qemu_params (
  cat $dir/vm.qemu $dir/$os.qemu $cdrom | sed '/^#/d' | sed '/^$/d' | string join " "
)

# Variable replacement
for qemu_var in $qemu_vars
  set qemu_params (string replace -a "{{$qemu_var}}" "$$qemu_var" $qemu_params)
end

# Run QEMU
eval (string split " " $qemu_params)
set qemu_pid (cat tmp/$MACHINE-qemu.pid)

while kill -0 $qemu_pid > /dev/null 2>&1
  sleep 1
end

# Shutdown TPM eumulator
$enable_tpm && pkill swtpm

# Bind PCI devices back to physical controllers
if $enable_vfio
  echo "Freeing VFIO binds"
  sudo $dir/vfio-bind $USB_CONTROLLER xhci_hcd 22
  if test "$os" = "windows"
    sudo $dir/vfio-bind $GAMES_SSD nvme 23
  end
end

# Ensure monitors come back on
light on